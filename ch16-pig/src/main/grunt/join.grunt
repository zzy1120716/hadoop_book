A = LOAD 'input/pig/join/A' AS (id:int, name:chararray);
B = LOAD 'input/pig/join/B' AS (name:chararray, id:int);

DUMP A;
DUMP B;

--内连接
C = JOIN A BY $0, B BY $1;
DUMP C;

--分段复制连接
C = JOIN A BY $0, B BY $1 USING 'replicated';
DUMP C;

--左外连接
C = JOIN A BY $0 LEFT OUTER, B BY $1;
DUMP C;

--使用COGROUP，嵌套输出元组集合，默认为外连接
D = COGROUP A BY $0, B BY $1;
DUMP D;

--COGROUP内连接
E = COGROUP A BY $0 INNER, B BY $1;
DUMP E;

--平面化
F = FOREACH E GENERATE FLATTEN(A), B.$0;
DUMP F;

--COGROUP,INNER,FLATTEN组合使用，相当于内连接
G = COGROUP A BY $0 INNER, B BY $1 INNER;
H = FOREACH G GENERATE FLATTEN($1), FLATTEN($2);
DUMP H;

--叉乘
I = CROSS A, B;
DUMP I;